class Vim
  eval: (vimL) ->
    return vim.api.nvim_eval(vimL)

  echo: (message) ->
    vim.api.nvim_out_write(message .. '\n')

  echoError: (message) ->
    vim.api.nvim_err_writeln(message)

  callFunction: (functionName, args) ->
    vim.api.nvim_call_function(functionName, args)

Assert = (condition, message) ->
  if not condition
    if message
      error("Assert hit! " .. message)
    else
      error("Assert hit!")

class Path
  join: (left, right) ->
    result = left
    lastChar = left\sub(-1)

    if lastChar != '/' and lastChar != '\\'
      result ..= '/'

    result ..= right
    return result

  normalize: (path) ->
    result = string.gsub(path, "\\", "/")

    if result\sub(-1) == '/'
      result = result\sub(0, #result - 1)

    return result

  makeMissingDirectoriesInPath: (path) ->
    dirPath = Path.getDirectory(path)
    Vim.callFunction('mkdir', { dirPath, 'p' })

  getDirectory: (path) ->
    return path\match('^(.*)[\\/][^\\/]*$')

class File
  exists: (path) ->
    return Vim.callFunction('filereadable', { path }) != 0

  getModificationTime: (path) ->
    return Vim.callFunction('getftime', { path })

  delete: (path) ->
    Vim.callFunction('delete', { path })

class Directory
  exists: (path) ->
    return Vim.callFunction('isdirectory', { path }) != 0

  getAllFilesWithExtensionRecursive: (path, extension) ->
    return [Path.normalize(x) for x in *Vim.callFunction('globpath', {path, "**/*.#{extension}", 0, 1})]

tableContains = (table, element) ->
  for value in *table
    if value == element then
      return true

  return false

deleteOrphanedLuaFiles = (validBaseNames, luaDir, verbose) ->
  for filePath in *Directory.getAllFilesWithExtensionRecursive(luaDir, 'lua')
    luaRelativePath = filePath\sub(#luaDir + 2)
    baseName = luaRelativePath\sub(0, #luaRelativePath - 4)

    if tableContains(validBaseNames, baseName)
      continue

    alwaysDelete = false
    if Vim.callFunction('exists', {'g:YueMakerDeleteOrphanedLuaFiles'}) == 1
      alwaysDelete = vim.api.nvim_get_var('YueMakerDeleteOrphanedLuaFiles')

    if not alwaysDelete
      choice = Vim.callFunction(
        "confirm", { "Lua file at '#{luaRelativePath}' does not have a corresponding Yue file. Delete it?\nNote that this popup can be suppressed with the g:YueMakerDeleteOrphanedLuaFiles setting", "&Delete\n&Skip\n&Abort", 2, "Question" })
      if choice == 2
        if verbose
          Vim.echo("Skipping orphaned lua file '#{luaRelativePath}'")
        continue
      if choice != 1
        if verbose
          Vim.echo("Aborting lua file cleanup")
        return

    File.delete(filePath)
    if verbose
      Vim.echo("Deleted file '#{filePath}' since it had no matching yue file")

timeStampIsGreater = (file1Path, file2Path) ->
    time1 = File.getModificationTime(file1Path)
    time2 = File.getModificationTime(file2Path)

    return time1 > time2

class YueMaker
  getCompiler: () ->
      if Vim.callFunction('exists', {'g:YueCompiler'}) == 1
          return vim.api.nvim_get_var('YueCompiler')
      else
          return "yue"

  executeYue: (yueText) ->
    luaText = Vim.callFunction("system", { YueMaker.getCompiler() .. " --", yueText })
    loadstring(luaText)!

  -- Returns true if it was compiled
  compileYueIfOutOfDate: (yuePath, luaPath) ->
    if not File.exists(luaPath) or timeStampIsGreater(yuePath, luaPath)
      Path.makeMissingDirectoriesInPath(luaPath)
      output = Vim.callFunction("system", { YueMaker.getCompiler() .. " -o \"#{luaPath}\" \"#{yuePath}\"" })

      if Vim.eval('v:shell_error') != 0
        Vim.echoError("Errors occurred while compiling file '#{yuePath}'")
        Vim.echoError(output)
        return false

      return true

    return false

  compileDir: (yueDir, luaDir, verbose) ->
    numUpdated = 0
    yueBaseNames = {}

    for filePath in *Directory.getAllFilesWithExtensionRecursive(yueDir, 'yue')
      baseName = filePath\sub(#yueDir + 2)
      baseName = baseName\sub(0, #baseName - 4)
      table.insert(yueBaseNames, baseName)

    if #yueBaseNames > 0
      deleteOrphanedLuaFiles(yueBaseNames, luaDir, verbose)

      for baseName in *yueBaseNames
        luaPath = Path.join(luaDir, baseName) .. '.lua'
        yuePath = Path.join(yueDir, baseName) .. '.yue'

        if YueMaker.compileYueIfOutOfDate(yuePath, luaPath)
          if verbose
            Vim.echo("Compiled file '#{yuePath}'")

          -- Also delete it from the package cache so the next time require(baseName)
          -- is called, it will load the new file
          packageName = baseName\gsub("\\", ".")\gsub("/", ".")
          package.loaded[packageName] = nil
          numUpdated += 1
    return numUpdated

  compileAllYueFilesInDirectoryRecursive: (rootDir) ->
    numUpdated = 0
    for yuePath in *Directory.getAllFilesWithExtensionRecursive(rootDir, 'yue')
      luaPath = yuePath\sub(0, #yuePath - 4) .. "lua"
      if YueMaker.compileYueIfOutOfDate(yuePath, luaPath)
        numUpdated += 1
    return numUpdated

  compileAll: (verbose) ->
    rtp = Vim.eval('globpath(&rtp, "", 0, 1)')
    paths = [Path.normalize(x) for _, x in ipairs(rtp)]

    numUpdated = 0

    for pluginRoot in *paths
      yueDir = Path.join(pluginRoot, 'yue')
      luaDir = Path.join(pluginRoot, 'lua')

      numUpdated += YueMaker.compileDir(yueDir, luaDir, verbose)

      for dirName in *{'ftplugin', 'indent', 'plugin'}
        numUpdated += YueMaker.compileAllYueFilesInDirectoryRecursive(Path.join(pluginRoot, dirName))

    if verbose and numUpdated == 0
      Vim.echo("All yue files are already up to date")

    return numUpdated

